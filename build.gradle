buildscript {
    repositories {
        maven {
            url("$artifactoryLibraryRepo")
            credentials {
                username = "$artifactoryUsername"
                password = "$artifactoryPassword"
            }
        }
    }
	
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.0.1")
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "groovy"
apply plugin: "spring-boot"
apply plugin: "org.sonarqube"
apply plugin: "maven-publish"
apply plugin: "project-report"
apply from: file("gradle/docker.gradle")
apply from: file("gradle/versioning.gradle")

jar {
    baseName = "$project.name"
    version =  "$version"
}

repositories {
    maven {
        url("$artifactoryLibraryRepo")

        credentials {
            username = "$artifactoryUsername"
            password = "$artifactoryPassword"
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

sonarqube {
    properties {
        property "sonar.host.url", "$sonarUrl"
        property "sonar.login", "$sonarUsername"
        property "sonar.password", "$sonarPassword"
        property "sonar.projectName", "$project.name"
        property "sonar.projectKey", "$group.$project.name"
        property "sonar.links.ci", "$System.env.BUILD_URL"
        property "sonar.links.scm", "$System.env.GIT_URL"
        property "sonar.jacoco.reportPath", "$buildDir.name/jacoco.exec"
        property "sonar.java.coveragePlugin", "jacoco"
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifactId "$project.name"

            pom.withXml {
                def root = asNode()
                root.appendNode("name", "$project.name")
                root.appendNode("description", "$project.name")
                root.appendNode("inceptionYear", '2016')

                def license = root.appendNode("licenses").appendNode("license")
                license.appendNode("name", "The Apache Software License, Version 2.0")
                license.appendNode("url", "http://www.apache.org/licenses/LICENSE-2.0.txt")
                license.appendNode("distribution", "repo")

                def developer = root.appendNode("developers").appendNode("developer")
                developer.appendNode("id", "khainguyentuong")
                developer.appendNode("name", "Khai Nguyen")
                developer.appendNode("email", "khainguyentuong@gmail.com")
            }
        }
    }

    repositories {
        maven {
            url("$artifactoryReleaseRepo")

            credentials {
                username = "$artifactoryUsername"
                password = "$artifactoryPassword"
            }
        }
    }
}

task allDeps(type: DependencyReportTask) {}

htmlDependencyReport {
    projects = project.allprojects
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.14"
}

defaultTasks("clean", "publish", "sonarqube", "htmlDependencyReport")
